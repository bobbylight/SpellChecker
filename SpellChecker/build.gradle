['distribution', 'maven-publish', 'signing'].each { apply plugin: it }

assert JavaVersion.current().isJava8Compatible()

archivesBaseName = 'spellchecker'
ext.isReleaseVersion = !project.version.endsWith('SNAPSHOT')

ext.sharedManifest = manifest {
	attributes('Class-Path': 'rsyntaxtextarea.jar',
		'Specification-Title': 'SpellChecker',
		'Specification-Version': version,
		'Implementation-Title': 'org.fife.ui',
		'Implementation-Version': version)

}

java {
    withSourcesJar()
    withJavadocJar()
}
jar {
	manifest { from sharedManifest }
}

// We use "distributions" to create the zip files uploaded to SourceForge
distributions {
	main {
        distributionBaseName = 'rsta_spellchecker'
		contents {
			from { [ 'build/libs' ] }
			rename 'SpellChecker-.*\\.jar', 'rsta_spellchecker.jar'
		}
	}
	src {
        distributionBaseName = 'rsta_spellchecker'
	}
}
distZip.classifier = null
distZip.dependsOn jar
srcDistZip.classifier = 'src'
srcDistZip {
	from projectDir
	include 'src/**/*'
	include 'build.gradle'
	include '.classpath'
	include '.project'
	include 'gradle.properties'
	include 'gradle/**/*'
	include 'gradlew*'
	include 'README.md'
	include '.settings/**'
}

task buildSourceForgeZips {
    doLast {
        println 'Building zip files for SourceForge'
    }
}
buildSourceForgeZips.dependsOn clean, jar, distZip, srcDistZip

repositories {
	mavenCentral()
}
publishing {
	repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            credentials { // Credentials usually kept in user's .gradle/gradle.properties
                // We must defensively check for these properties so Travis CI build works
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : 'unknown'
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : 'unknown'
            }
        }
    }
    publications {
        maven(MavenPublication) {

            groupId = 'com.fifesoft'
            artifactId = 'spellchecker'
            version = version

            from components.java

            pom {

                name = 'SpellChecker'
                description = 'A simple spell checker add-on for RSyntaxTextArea. It will spell-check comments in source code, or the entire file if you are editing plain text. Spelling errors are squiggle-underlined with the color of your choice, and tooltips are available offering any spelling suggestions.'
                url = 'http://www.fifesoft.com/rsyntaxtextarea/'
                inceptionYear = '2003'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'Modified BSD License'
                        url = 'http://fifesoft.com/rsyntaxtextarea/RSyntaxTextArea.License.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Robert Futrell'
                    }
                }
                scm {
                    url = 'https://github.com/bobbylight/SpellChecker'
                    connection = 'scm:git:git://github.com/bobbylight/SpellChecker'
                    developerConnection = 'scm:git:git@github.com:bobbylight/SpellChecker'
                    if (!project.version.endsWith('-SNAPSHOT')) {
                        tag = project.version
                    }
                }
            }
        }
	}
}

signing {
    // Don't require signing for e.g. ./gradlew install
    required { gradle.taskGraph.hasTask('publish') && isReleaseVersion }
    sign publishing.publications.maven
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
